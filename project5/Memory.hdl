// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    //\\ RAM:
    // 000 ...
    // 001 ...
    // 010 ...
    // 011 ...
    //\\ SCREEN:
    // 100 ...
    // 101 ...
    //\\ KBD:
    // 110 ...
    DMux8Way(in=load, sel=address[12..14],
        a=ramB0, b=ramB1, c=ramB2, d=ramB3,
        e=screenB0, f=screenB1,
        g=kbdB,
        h=mock);

    Keyboard(out=kbd);

    Screen(in=in, load=screenLoad, address=address[0..12], out=screen);
    Or(a=screenB0, b=screenB1, out=screenLoad);

    RAM16K(in=in, load=ramLoad, address=address[0..13], out=ram);
    Or(a=ramB0, b=ramB1, out=ramBOR0);
    Or(a=ramB2, b=ramB3, out=ramBOR1);
    Or(a=ramBOR0, b=ramBOR1, out=ramLoad);
    
    Mux8Way16(
        a=ram, b=ram, c=ram, d=ram,
        e=screen, f=screen,
        g=kbd,
        h=false, 
    sel=address[12..14], out=out);
}